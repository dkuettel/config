#!/bin/zsh
set -eux -o pipefail

this=$0
continue_from=${1:-}

cd ${0:a:h}

source ../zsh/path.zsh

# NOTE starting with 20.04 there seem to be no more xserver-xorg-hwe-xx.xx
# see https://ubuntu.com/kernel/lifecycle
# sudo apt-get install -qy --install-recommends xserver-xorg-hwe-20.04

sudo apt-get install -qqy xinit # for startx

# this run-target should prevent greeters from appearing
sudo systemctl set-default multi-user.target
# but to be sure disabling greeting as well (if they exist)
sudo systemctl disable gdm.service || true
sudo systemctl stop gdm.service || true


# apt installs (sorted)
i=(
    #kdenlive  # seems ok for cutting videos
    cool-retro-term
    d-feet  # introspect d-bus, dbus
    evince  # for the easier pdfs (when not using okular)
    feh  # note also .xsession uses feh, is that the only way?
    firefox  # NOTE in 22.04 this actually installs a snap
    geeqie  # pulls in many dependencies because of kde
    gnome-sushi  # nautilus plugin for preview on space-key
    gparted  # seems a bit better than "partitionmanager"
    gpick  # easy color picker from anything on the screen, useful when debugging terminal colors
    gthumb  # together with scrot
    maim  # for screenshot directly into copy buffer
    nautilus  # filemanager gui
    qdirstat  # disk space explorer (only gui)
    scrot  # screenshots
    transmission-gtk  # NOTE keeps seetings in ~/.config/transmission/settings.json; could be configured if not specific for machine
    xclip
    xdotool  # for xsession and spotify title
    yaru-theme-icon  # gnome apps currently seem to use that one and dont depend on it when installing, might change again
)
sudo apt-get install -qy $i

# nautilus, and probably other things, install the "tracker-miner-fs" stuff, indexing files for a quick search
# sometimes this service delayed the shutdown, but also I'm not really using it
# 'systemctl mask' does not just disable, but also make it impossible to enable, until you 'unmask'
# masking wildly everything here
# see https://www.linuxuprising.com/2019/07/how-to-completely-disable-tracker.html
tracker_units=(
    # tracker v2
    tracker-store.service tracker-miner-fs.service tracker-miner-rss.service tracker-extract.service tracker-miner-apps.service tracker-writeback.service
    # tracker v3
    tracker-extract-3.service tracker-miner-fs-3.service tracker-miner-rss-3.service tracker-writeback-3.service tracker-xdg-portal-3.service tracker-miner-fs-control-3.service
)
systemctl --user mask $tracker_units || true
systemctl --user stop $tracker_units || true

# snap installs (sorted)
# i=(
# )
# sudo snap install $i

# ordered list of custom setups
# only the unattended ones
# see ./attended-setup for others
setups=(
    redshift
    keyboard # probably not doing the right thing in virtual box
    dwm # sometimes '/dev/fb0: Permission denied' with startx, probably just bad drivers
    dmenu
    bluetooth
    fonts
    alacritty
    sound
    okular
    vlc
    ltstatus
    pstart
)

if [[ $continue_from != '' ]]; then
    at=$setups[(i)$continue_from]
    if [[ $at -gt $#setups ]]; then
        echo "Cannot find $continue_from to continue from." 1>&2
        exit 1
    fi
    setups=$setups[$at,-1]
fi

function TRAPZERR {
    echo 'Setup failed for "'$i'". Fix and then resume with "'$this $i'".'
}
set +x
for i in $setups; do
    ../$i/setup
done
set -x
unset -f TRAPZERR

echo 'Use i/desktop/attended-setup when in X.'
